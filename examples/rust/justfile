# Rust example gRPC servers for angzarr e-commerce domain
# Services: customer, product, inventory, order, cart, fulfillment
# Sagas: saga-loyalty-earn, saga-fulfillment, saga-cancellation

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE_TAG := "latest"
REGISTRY_PORT := "5001"

# Show available commands
default:
    @just --list

# ============================================================================
# Build / Test / Clean (workspace-level)
# ============================================================================

# Build all Rust example crates
build:
    cargo build --workspace

# Run unit tests (inline lib tests, no cluster required)
test:
    cargo test --workspace --lib

# Run integration tests (deploys to Kind cluster, runs technical tests)
integration: setup
    skaffold run
    @echo "Waiting for RabbitMQ..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=rabbitmq -n angzarr --timeout=120s || true
    @echo "Waiting for MongoDB..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=mongodb -n angzarr --timeout=120s || true
    @echo "Waiting for services to be ready..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=angzarr -n angzarr --timeout=300s || true
    TEST_LANGUAGE=rust cargo test --test container_integration --manifest-path "{{TOP}}/Cargo.toml"

# Run acceptance tests (deploys to Kind cluster, runs BDD cucumber tests)
acceptance: setup
    skaffold run
    @echo "Waiting for RabbitMQ..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=rabbitmq -n angzarr --timeout=120s || true
    @echo "Waiting for MongoDB..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=mongodb -n angzarr --timeout=120s || true
    @echo "Waiting for services to be ready..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/name=angzarr -n angzarr --timeout=300s || true
    cargo test --workspace --test acceptance

# Clean build artifacts
clean:
    cargo clean

# Format all code
fmt:
    cargo fmt --all

# Lint all code
lint:
    cargo clippy --workspace -- -D warnings

# Lint Helm chart
helm-lint:
    helm lint helm

# ============================================================================
# Container Images
# ============================================================================

# Build all Rust example images (uses multi-stage Containerfile)
images:
    podman build --target builder -f Containerfile "{{TOP}}"
    podman build --target customer -t localhost:{{REGISTRY_PORT}}/entity-customer-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target product -t localhost:{{REGISTRY_PORT}}/entity-product-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target inventory -t localhost:{{REGISTRY_PORT}}/entity-inventory-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target order -t localhost:{{REGISTRY_PORT}}/entity-order-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target cart -t localhost:{{REGISTRY_PORT}}/entity-cart-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target fulfillment -t localhost:{{REGISTRY_PORT}}/entity-fulfillment-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-loyalty-earn -t localhost:{{REGISTRY_PORT}}/saga-loyalty-earn-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-fulfillment -t localhost:{{REGISTRY_PORT}}/saga-fulfillment-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-cancellation -t localhost:{{REGISTRY_PORT}}/saga-cancellation-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"

# Build a single service image
image SERVICE:
    podman build --target {{SERVICE}} -t localhost:{{REGISTRY_PORT}}/entity-{{SERVICE}}-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"

# Push all images to local registry
push:
    podman push localhost:{{REGISTRY_PORT}}/entity-customer-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/entity-product-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/entity-inventory-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/entity-order-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/entity-cart-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/entity-fulfillment-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-loyalty-earn-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-fulfillment-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-cancellation-rs:{{IMAGE_TAG}} --tls-verify=false

# ============================================================================
# Skaffold Development (recommended workflow)
# ============================================================================

# One-time setup: configure Podman and Skaffold for local registry
setup:
    @echo "Configuring Podman for local registry..."
    @uv run "{{TOP}}/scripts/configure_podman_registry.py"
    @echo "Configuring Skaffold for Kind..."
    @uv run "{{TOP}}/scripts/configure_skaffold.py"
    uv run "{{TOP}}/scripts/kind-with-registry.py"

# Start skaffold dev loop (watches files, rebuilds on change)
dev: setup
    cd "{{TOP}}" && just secrets-init
    skaffold dev

# Build and deploy once
run: setup
    cd "{{TOP}}" && just secrets-init
    skaffold run

# Build images only (no deploy)
build-images:
    skaffold build

# Delete deployed resources
delete:
    skaffold delete

# ============================================================================
# Run Individual Services (local development without K8s)
# ============================================================================

# Run customer server (port 50055)
run-customer:
    cargo run -p customer --bin customer-server

# Run product server (port 50053)
run-product:
    cargo run -p product --bin product-server

# Run inventory server (port 50054)
run-inventory:
    cargo run -p inventory --bin inventory-server

# Run order server (port 50056)
run-order:
    cargo run -p order --bin order-server

# Run cart server (port 50057)
run-cart:
    cargo run -p cart --bin cart-server

# Run fulfillment server (port 50058)
run-fulfillment:
    cargo run -p fulfillment --bin fulfillment-server

# Run saga-loyalty-earn server (port 50060)
run-saga-loyalty-earn:
    cargo run -p saga-loyalty-earn --bin saga-loyalty-earn-server

# Run saga-fulfillment server (port 50061)
run-saga-fulfillment:
    cargo run -p saga-fulfillment --bin saga-fulfillment-server

# Run saga-cancellation server (port 50062)
run-saga-cancellation:
    cargo run -p saga-cancellation --bin saga-cancellation-server

# ============================================================================
# Aliases (backwards compatibility)
# ============================================================================

skaffold-init: setup
skaffold-setup: setup
skaffold-dev: dev
skaffold-run: run
skaffold-build: build-images
skaffold-delete: delete
