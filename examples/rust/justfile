# Rust example gRPC servers for angzarr e-commerce domain
# Services: customer, product, inventory, order, cart, fulfillment
# Sagas: saga-loyalty-earn, saga-fulfillment, saga-cancellation
#
# Deployment Model:
#   - Backing services (PostgreSQL, RabbitMQ): Deployed via Terraform/Helm (see root justfile)
#   - Application services: Deployed via Skaffold (this justfile)
#
# For integration/acceptance tests, run from root: `just integration` or `just acceptance`
# This ensures backing services are deployed first via Terraform.

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE_TAG := "latest"
REGISTRY_PORT := "5001"

# Show available commands
default:
    @just --list

# ============================================================================
# Build / Test / Clean (workspace-level)
# ============================================================================

# Build all Rust example crates (includes angzarr with sqlite)
build:
    cargo build --workspace --features sqlite

# Run unit tests (inline lib tests, no cluster required)
test:
    cargo test --workspace --lib

# Run integration tests (assumes backing services already deployed via root `just infra-local`)
# Deploy application services via Skaffold, then run technical tests
integration:
    skaffold run
    @echo "Waiting for application services to be ready..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/component=aggregate -n angzarr --timeout=300s || true
    ANGZARR_PORT=1350 \
    TEST_LANGUAGE=rust cargo test --test container_integration --manifest-path "{{TOP}}/Cargo.toml"

# Run acceptance tests (assumes backing services already deployed via root `just infra-local`)
# Deploy application services via Skaffold, then run BDD cucumber tests
acceptance:
    skaffold run
    @echo "Waiting for application services to be ready..."
    @kubectl wait --for=condition=ready pod -l app.kubernetes.io/component=aggregate -n angzarr --timeout=300s || true
    ANGZARR_PORT=1350 \
    cargo test --workspace --test acceptance

# Clean build artifacts
clean:
    cargo clean

# Format all code
fmt:
    cargo fmt --all

# Lint all code
lint:
    cargo clippy --workspace -- -D warnings

# Lint Helm chart
helm-lint:
    helm lint helm

# ============================================================================
# Container Images
# ============================================================================

# Build all Rust example images (uses multi-stage Containerfile)
images:
    podman build --target builder -f Containerfile "{{TOP}}"
    podman build --target customer -t localhost:{{REGISTRY_PORT}}/aggregate-customer-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target product -t localhost:{{REGISTRY_PORT}}/aggregate-product-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target inventory -t localhost:{{REGISTRY_PORT}}/aggregate-inventory-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target order -t localhost:{{REGISTRY_PORT}}/aggregate-order-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target cart -t localhost:{{REGISTRY_PORT}}/aggregate-cart-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target fulfillment -t localhost:{{REGISTRY_PORT}}/aggregate-fulfillment-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-loyalty-earn -t localhost:{{REGISTRY_PORT}}/saga-loyalty-earn-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-fulfillment -t localhost:{{REGISTRY_PORT}}/saga-fulfillment-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"
    podman build --target saga-cancellation -t localhost:{{REGISTRY_PORT}}/saga-cancellation-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"

# Build a single service image
image SERVICE:
    podman build --target {{SERVICE}} -t localhost:{{REGISTRY_PORT}}/entity-{{SERVICE}}-rs:{{IMAGE_TAG}} -f Containerfile "{{TOP}}"

# Push all images to local registry
push:
    podman push localhost:{{REGISTRY_PORT}}/aggregate-customer-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/aggregate-product-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/aggregate-inventory-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/aggregate-order-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/aggregate-cart-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/aggregate-fulfillment-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-loyalty-earn-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-fulfillment-rs:{{IMAGE_TAG}} --tls-verify=false
    podman push localhost:{{REGISTRY_PORT}}/saga-cancellation-rs:{{IMAGE_TAG}} --tls-verify=false

# ============================================================================
# Skaffold Development (recommended workflow)
# ============================================================================
#
# UNIFIED WORKFLOW:
#   just dev    - watch mode, rebuilds on file change (framework + business)
#   just run    - build and deploy once
#
# All 14 images (5 framework + 9 business) are built with sha256 tags.
# Skaffold caches aggressively - only changed images are rebuilt.
#
# With lefthook: commits automatically trigger deployment (see lefthook.yml)

# One-time setup: configure Podman and Skaffold for local registry
setup:
    @echo "Configuring Podman for local registry..."
    @uv run "{{TOP}}/scripts/configure_podman_registry.py"
    @echo "Configuring Skaffold for Kind..."
    @uv run "{{TOP}}/scripts/configure_skaffold.py"
    uv run "{{TOP}}/scripts/kind-with-registry.py"

# Start skaffold dev loop (watches files, rebuilds on change)
# Builds all 14 images (framework + business) via 'requires' directive
dev: setup
    cd "{{TOP}}" && just secrets-init
    @echo "=== Skaffold Dev Mode ==="
    @echo "Building all images (5 framework + 9 business) with sha256 tags"
    @echo "Only changed images will be rebuilt (skaffold caching)"
    @echo ""
    skaffold dev

# Build and deploy once
run: setup
    cd "{{TOP}}" && just secrets-init
    @echo "=== Skaffold Deploy ==="
    @echo "Building all images with sha256 tags"
    skaffold run

# Build images only (no deploy)
build-images:
    skaffold build

# Delete deployed resources
delete:
    skaffold delete

# ============================================================================
# Embedded Mode (Local Development with UDS)
# Runs all services via angzarr-embedded orchestrator
# ============================================================================

# Build angzarr binaries for embedded mode
build-angzarr:
    cd {{TOP}} && cargo build --features sqlite --bin angzarr-aggregate --bin angzarr-saga --bin angzarr-projector --bin angzarr-gateway --bin angzarr_embedded

# Clean up stale UDS sockets
clean-sockets:
    mkdir -p /tmp/angzarr
    rm -f /tmp/angzarr/*.sock 2>/dev/null || true

# Run all services in embedded mode (single command)
embedded: build-angzarr clean-sockets build
    cd {{TOP}}/examples/rust && ANGZARR_CONFIG=config/embedded.yaml {{TOP}}/target/debug/angzarr_embedded

# Kill any stale embedded processes
kill-embedded:
    #!/usr/bin/env bash
    # Use pgrep to find exact binary matches, then kill
    for proc in angzarr_embedded angzarr-aggregate angzarr-saga angzarr-projector angzarr-gateway; do
        pids=$(pgrep -x "$proc" 2>/dev/null || true)
        if [ -n "$pids" ]; then
            echo "$pids" | xargs kill -9 2>/dev/null || true
        fi
    done
    sleep 1

# Run acceptance tests against embedded mode
# Starts embedded runtime in background, runs tests, cleans up
acceptance-embedded: build-angzarr kill-embedded clean-sockets build
    #!/usr/bin/env bash
    set -e
    cd "{{TOP}}/examples/rust"

    # Cleanup function
    cleanup() {
        echo "Cleaning up..."
        for proc in angzarr_embedded angzarr-aggregate angzarr-saga angzarr-projector angzarr-gateway; do
            pids=$(pgrep -x "$proc" 2>/dev/null || true)
            if [ -n "$pids" ]; then
                echo "$pids" | xargs kill -9 2>/dev/null || true
            fi
        done
        rm -f /tmp/angzarr/*.sock 2>/dev/null || true
    }
    trap cleanup EXIT

    # Start embedded runtime in background
    echo "Starting embedded runtime..."
    ANGZARR_CONFIG=config/embedded.yaml "{{TOP}}/target/debug/angzarr_embedded" &
    EMBEDDED_PID=$!

    # Wait for services to be ready (gateway on port 1350)
    # Use TCP connection check (no reflection needed)
    echo "Waiting for gateway to be ready..."
    for i in {1..60}; do
        if nc -z localhost 1350 2>/dev/null; then
            echo "Gateway ready!"
            break
        fi
        if [ $i -eq 60 ]; then
            echo "Timeout waiting for gateway"
            exit 1
        fi
        sleep 1
    done

    # Run acceptance tests
    echo "Running acceptance tests..."
    ANGZARR_PORT=1350 cargo test --workspace --test acceptance || TEST_RESULT=$?

    exit ${TEST_RESULT:-0}

# ============================================================================
# Run Individual Services (local development without K8s)
# See docs/port-conventions.md for port scheme (logic port = base + 3)
# ============================================================================

# Run customer server (logic port 50053)
run-customer:
    cargo run -p customer --bin customer-server

# Run product server (logic port 50063)
run-product:
    cargo run -p product --bin product-server

# Run inventory server (logic port 50073)
run-inventory:
    cargo run -p inventory-svc --bin inventory-server

# Run order server (logic port 50083)
run-order:
    cargo run -p order --bin order-server

# Run cart server (logic port 50093)
run-cart:
    cargo run -p cart --bin cart-server

# Run fulfillment server (logic port 50103)
run-fulfillment:
    cargo run -p fulfillment --bin fulfillment-server

# Run saga-loyalty-earn server (logic port 50113)
run-saga-loyalty-earn:
    cargo run -p saga-loyalty-earn --bin saga-loyalty-earn-server

# Run saga-fulfillment server (logic port 50123)
run-saga-fulfillment:
    cargo run -p saga-fulfillment --bin saga-fulfillment-server

# Run saga-cancellation server (logic port 50133)
run-saga-cancellation:
    cargo run -p saga-cancellation --bin saga-cancellation-server

# ============================================================================
# Aliases (backwards compatibility)
# ============================================================================

skaffold-init: setup
skaffold-setup: setup
skaffold-dev: dev
skaffold-run: run
skaffold-build: build-images
skaffold-delete: delete
