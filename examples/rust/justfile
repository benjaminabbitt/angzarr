# Rust example gRPC servers
# Container: podman build -t angzarr-dev-rust -f examples/rust/Containerfile.dev .
# Run:       podman run --rm -v .:/app:Z -w /app/examples/rust angzarr-dev-rust just test

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Build dev container
container-build:
    cd {{TOP}} && podman build -t angzarr-dev-rust -f examples/rust/Containerfile.dev .

# Run tests in container
container-test:
    cd {{TOP}} && podman run --rm -v .:/app:Z -w /app/examples/rust angzarr-dev-rust just test

# === Build ===

# Build all Rust example servers
build: build-customer build-transaction build-saga-loyalty build-projector-receipt build-projector-log-customer build-projector-log-transaction

# Build customer server
build-customer:
    cd customer && just build

# Build transaction server
build-transaction:
    cd transaction && just build

# Build saga-loyalty server
build-saga-loyalty:
    cd saga-loyalty && just build

# Build projector-receipt server
build-projector-receipt:
    cd projector-receipt && just build

# Build projector-log-customer server
build-projector-log-customer:
    cd projector-log-customer && just build

# Build projector-log-transaction server
build-projector-log-transaction:
    cd projector-log-transaction && just build

# === Test ===

# Test all Rust examples
test: test-customer test-transaction test-saga-loyalty test-projector-receipt test-projector-log-customer test-projector-log-transaction

# Test customer
test-customer:
    cd customer && just test

# Test transaction
test-transaction:
    cd transaction && just test

# Test saga-loyalty
test-saga-loyalty:
    cd saga-loyalty && just test

# Test projector-receipt
test-projector-receipt:
    cd projector-receipt && just test

# Test projector-log-customer
test-projector-log-customer:
    cd projector-log-customer && just test

# Test projector-log-transaction
test-projector-log-transaction:
    cd projector-log-transaction && just test

# === Clean ===

# Clean all Rust examples
clean:
    cd common && just clean || true
    cd customer && just clean || true
    cd transaction && just clean || true
    cd saga-loyalty && just clean || true
    cd projector-receipt && just clean || true
    cd projector-log-customer && just clean || true
    cd projector-log-transaction && just clean || true

# === Format ===

# Format all Rust examples
fmt:
    cd customer && cargo fmt
    cd transaction && cargo fmt
    cd saga-loyalty && cargo fmt
    cd projector-receipt && cargo fmt
    cd projector-log-customer && cargo fmt
    cd projector-log-transaction && cargo fmt

# === Lint ===

# Lint all Rust examples
lint:
    cd customer && cargo clippy -- -D warnings
    cd transaction && cargo clippy -- -D warnings
    cd saga-loyalty && cargo clippy -- -D warnings
    cd projector-receipt && cargo clippy -- -D warnings
    cd projector-log-customer && cargo clippy -- -D warnings
    cd projector-log-transaction && cargo clippy -- -D warnings

# Lint Helm chart
helm-lint:
    helm lint helm

# === Run ===

# Run customer server (default port 50052)
run-customer:
    cd customer && just run

# Run transaction server (default port 50053)
run-transaction:
    cd transaction && just run

# Run saga-loyalty server (default port 50054)
run-saga-loyalty:
    cd saga-loyalty && just run

# Run projector-receipt server (default port 50055)
run-projector-receipt:
    cd projector-receipt && just run

# Run projector-log-customer server (default port 50056)
run-projector-log-customer:
    cd projector-log-customer && just run

# Run projector-log-transaction server (default port 50057)
run-projector-log-transaction:
    cd projector-log-transaction && just run

# === Skaffold Development (recommended workflow) ===

# One-time setup: configure Podman and Skaffold for local registry
setup:
    @echo "Configuring Podman for local registry..."
    @uv run "{{TOP}}/scripts/configure_podman_registry.py"
    @echo "Configuring Skaffold for Kind..."
    @uv run "{{TOP}}/scripts/configure_skaffold.py"
    uv run "{{TOP}}/scripts/kind-with-registry.py"

# Start skaffold dev loop (watches files, rebuilds on change)
dev: setup
    cd "{{TOP}}" && just secrets-init
    skaffold dev

# Build and deploy once
run: setup
    cd "{{TOP}}" && just secrets-init
    skaffold run

# Build images only (no deploy)
build-images:
    skaffold build

# Delete deployed resources
delete:
    skaffold delete

# Debug mode (with debugger support)
debug: setup
    cd "{{TOP}}" && just secrets-init
    skaffold debug

# Aliases for backwards compatibility
skaffold-init: setup
skaffold-setup: setup
skaffold-dev: dev
skaffold-run: run
skaffold-build: build-images
skaffold-delete: delete
skaffold-debug: debug
