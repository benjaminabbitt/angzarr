# Product Business Logic - Rust gRPC Server

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Build the server
build:
    cargo build -p product --bin product-server

# Build release
build-release:
    cargo build -p product --bin product-server --release

# Run the server
run: build
    cargo run -p product --bin product-server

# Run with custom port
run-port port:
    PORT={{port}} cargo run -p product --bin product-server

# Copy feature files from shared location
copy-features:
    rm -rf tests/features
    cp -r {{TOP}}/examples/features tests/

# Run unit tests (no infrastructure required)
unit:
    cargo test -p product --lib

# Run acceptance tests (requires Kind cluster)
acceptance: copy-features
    cargo test -p product --test acceptance

# Run all tests (unit + acceptance)
test: unit acceptance

# Run unit tests only (alias for CI without cluster)
test-unit: unit

# Clean build artifacts
clean:
    cargo clean -p product

# Check code
check:
    cargo check -p product

# Lint code
lint:
    cargo clippy -p product -- -D warnings
