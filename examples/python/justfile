# Python examples
# Container: podman build -t angzarr-dev-python -f examples/python/Containerfile.dev .
# Run:       podman run --rm -v .:/app:Z -w /app/examples/python angzarr-dev-python just test

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Build dev container
container-build:
    cd {{TOP}} && podman build -t angzarr-dev-python -f examples/python/Containerfile.dev .

# Run tests in container
container-test:
    cd {{TOP}} && podman run --rm -v .:/app:Z -w /app/examples/python angzarr-dev-python just test

# Setup all Python examples
setup: setup-common setup-customer setup-transaction setup-saga-loyalty setup-projector-receipt setup-projector-log-customer setup-projector-log-transaction

# Setup common utilities
setup-common:
    cd common && just setup

# Setup customer bounded context
setup-customer:
    cd customer && just setup

# Setup transaction bounded context
setup-transaction:
    cd transaction && just setup

# Setup saga-loyalty
setup-saga-loyalty:
    cd saga-loyalty && just setup

# Setup projector-receipt
setup-projector-receipt:
    cd projector-receipt && just setup

# Setup projector-log-customer
setup-projector-log-customer:
    cd projector-log-customer && just setup

# Setup projector-log-transaction
setup-projector-log-transaction:
    cd projector-log-transaction && just setup

# Test all Python examples
test: test-customer test-transaction test-saga-loyalty test-projector-receipt test-projector-log-customer test-projector-log-transaction

# Test customer
test-customer: setup-customer
    cd customer && just test

# Test transaction
test-transaction: setup-transaction
    cd transaction && just test

# Test saga-loyalty
test-saga-loyalty: setup-saga-loyalty
    cd saga-loyalty && just test

# Test projector-receipt
test-projector-receipt: setup-projector-receipt
    cd projector-receipt && just test

# Test projector-log-customer
test-projector-log-customer: setup-projector-log-customer
    cd projector-log-customer && just test

# Test projector-log-transaction
test-projector-log-transaction: setup-projector-log-transaction
    cd projector-log-transaction && just test

# Clean all Python examples
clean:
    cd common && just clean || true
    cd customer && just clean || true
    cd transaction && just clean || true
    cd saga-loyalty && just clean || true
    cd projector-receipt && just clean || true
    cd projector-log-customer && just clean || true
    cd projector-log-transaction && just clean || true

# Format all Python code
fmt:
    cd customer && uv run ruff format .
    cd transaction && uv run ruff format .
    cd saga-loyalty && uv run ruff format .
    cd projector-receipt && uv run ruff format .
    cd projector-log-customer && uv run ruff format .
    cd projector-log-transaction && uv run ruff format .

# Lint all Python code
lint:
    cd customer && uv run ruff check .
    cd transaction && uv run ruff check .
    cd saga-loyalty && uv run ruff check .
    cd projector-receipt && uv run ruff check .
    cd projector-log-customer && uv run ruff check .
    cd projector-log-transaction && uv run ruff check .

# Lint Helm chart
helm-lint:
    helm lint helm

# ============================================================================
# Integration Tests (deployed to Kind cluster)
# ============================================================================

# Setup integration tests
setup-integration:
    cd tests && uv sync

# Run container integration tests (requires running cluster)
integration: setup-integration
    cd tests && ANGZARR_TEST_MODE=container uv run pytest test_container_integration.py -v

# Run integration tests with custom gateway address
integration-custom GATEWAY: setup-integration
    cd tests && ANGZARR_TEST_MODE=container ANGZARR_GATEWAY={{GATEWAY}} uv run pytest test_container_integration.py -v
