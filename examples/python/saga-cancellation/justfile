# Cancellation Saga - Python gRPC Server

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

default:
    @just --list

install:
    uv sync --dev

proto:
    mkdir -p angzarr proto
    cp -r "{{TOP}}/generated/python/angzarr/"* angzarr/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/python/examples/"* proto/ 2>/dev/null || true
    touch angzarr/__init__.py proto/__init__.py

setup: install proto

run: setup
    uv run python server.py

run-port port:
    PORT={{port}} uv run python server.py

# Copy feature files from shared location
copy-features:
    rm -rf features
    cp -r {{TOP}}/examples/features .

# Run unit tests (no infrastructure required)
unit: setup
    uv run pytest -v --ignore=features/ --ignore=acceptance/

# Run acceptance tests (requires Kind cluster)
acceptance: setup copy-features
    ANGZARR_TEST_MODE=container uv run pytest -v features/

# Run all tests
test: unit acceptance

# Run unit tests only
test-unit: unit

clean:
    rm -rf __pycache__ .pytest_cache angzarr/*.py proto/*.py
    find . -type d -name __pycache__ -exec rm -rf {} + 2>/dev/null || true
