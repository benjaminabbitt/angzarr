# Transaction Business Logic (Python)

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Install dependencies
install:
    uv venv --quiet 2>/dev/null || true
    uv pip install --quiet protobuf>=5.0.0 pytest>=8.0.0

# Copy generated proto files
proto:
    mkdir -p evented/proto proto
    cp -r "{{TOP}}/generated/python/evented/"* evented/proto/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/python/examples/"* proto/ 2>/dev/null || true
    touch evented/__init__.py evented/proto/__init__.py proto/__init__.py

# Full setup
setup: install proto

# Run tests
test: setup
    .venv/bin/python -m pytest -v

# Clean
clean:
    rm -rf __pycache__ evented/proto/*.py proto/*.py .venv
    find . -type d -name __pycache__ -exec rm -rf {} + 2>/dev/null || true
