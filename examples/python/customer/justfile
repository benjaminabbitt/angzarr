# Customer Business Logic - Python gRPC Server

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Install dependencies
install:
    uv sync --dev

# Copy generated proto files
proto:
    mkdir -p angzarr/proto proto
    cp -r "{{TOP}}/generated/python/angzarr/"* angzarr/proto/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/python/examples/"* proto/ 2>/dev/null || true
    touch angzarr/__init__.py angzarr/proto/__init__.py proto/__init__.py

# Full setup
setup: install proto

# Run the server
run: setup
    uv run python server.py

# Run with debugpy (for remote debugging)
debug: setup
    uv run python -m debugpy --listen 0.0.0.0:5678 --wait-for-client server.py

# Run with custom port
run-port port:
    PORT={{port}} uv run python server.py

# Copy feature files from shared location
copy-features:
    rm -rf features
    cp -r {{TOP}}/examples/features .

# Run unit tests (no infrastructure required)
unit: setup
    uv run pytest -v test_customer_logic.py

# Run acceptance tests (requires Kind cluster)
acceptance: setup copy-features
    ANGZARR_TEST_MODE=container uv run pytest -v features/

# Run all tests
test: unit acceptance

# Run unit tests only
test-unit: unit

# Clean
clean:
    rm -rf __pycache__ .pytest_cache angzarr/proto/*.py proto/*.py
    find . -type d -name __pycache__ -exec rm -rf {} + 2>/dev/null || true
