# Angzarr examples - multi-language gRPC implementations
# Language-specific commands are in their respective justfiles (e.g., rust/justfile)

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Import language-specific justfiles as modules
mod rust "rust/justfile"
mod go "go/justfile"
mod python "python/justfile"
mod csharp "csharp/justfile"
mod java "java/justfile"
mod kotlin "kotlin/justfile"
mod ruby "ruby/justfile"
mod typescript "typescript/justfile"

# Show available commands
default:
    @just --list

# ============================================================================
# Proto Management (cross-language)
# ============================================================================

# Copy canonical proto files to example directories (required before building)
proto-copy:
    @mkdir -p "{{TOP}}/examples/go/proto"
    @mkdir -p "{{TOP}}/examples/python/proto"
    @mkdir -p "{{TOP}}/examples/csharp/proto"
    @mkdir -p "{{TOP}}/examples/kotlin/proto"
    @mkdir -p "{{TOP}}/examples/typescript/proto"
    @mkdir -p "{{TOP}}/examples/ruby/proto"
    @mkdir -p "{{TOP}}/examples/java/proto/angzarr"
    @mkdir -p "{{TOP}}/examples/java/proto/examples"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/go/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/python/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/csharp/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/kotlin/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/typescript/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/ruby/proto/"
    cp "{{TOP}}/proto/examples/domains.proto" "{{TOP}}/examples/java/proto/examples/"
    cp "{{TOP}}/proto/angzarr/angzarr.proto" "{{TOP}}/examples/java/proto/angzarr/"

# Generate protos (required before building non-Rust examples)
proto: proto-copy
    cd "{{TOP}}" && just proto-generate

# ============================================================================
# Skaffold Shortcuts (delegates to language modules)
# ============================================================================

# Start examples with skaffold dev (file watching)
dev LANG:
    cd "{{TOP}}/examples/{{LANG}}" && skaffold dev

# Build and deploy examples once
run LANG:
    cd "{{TOP}}/examples/{{LANG}}" && skaffold run

# Delete skaffold deployment
delete LANG:
    cd "{{TOP}}/examples/{{LANG}}" && skaffold delete

# ============================================================================
# Cross-Language Operations
# ============================================================================

# Clean all examples
clean:
    just rust clean || true
    just go clean || true
    just python clean || true
    just csharp clean || true
    just java clean || true
    just kotlin clean || true
    just ruby clean || true
    just typescript clean || true

# Format all examples
fmt:
    just rust fmt || true
    just go fmt || true
    just python fmt || true

# Lint all examples
lint:
    just rust lint || true
    just go lint || true
    just python lint || true

# Lint all Helm charts
helm-lint:
    just rust helm-lint || true
    just go helm-lint || true
    just python helm-lint || true
    just csharp helm-lint || true
    just java helm-lint || true
    just kotlin helm-lint || true
    just ruby helm-lint || true
    just typescript helm-lint || true

# ============================================================================
# Container Cache Management
# ============================================================================

# Show podman disk usage
cache-status:
    @echo "=== Podman System Disk Usage ==="
    podman system df
    @echo ""
    @echo "=== Example Images ==="
    podman images --filter "reference=*entity-*" --filter "reference=*saga-*"

# Prune unused images
cache-prune:
    @echo "Pruning dangling images..."
    podman image prune -f
    @echo ""
    podman system df
