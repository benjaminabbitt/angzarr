# Loyalty Points Saga (Go)

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Copy generated proto files
proto:
    mkdir -p proto/evented proto/examples
    cp -r "{{TOP}}/generated/go/evented/"* proto/evented/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/go/examples/"* proto/examples/ 2>/dev/null || true

# Build shared library for FFI
build: proto
    go mod tidy
    go build -buildmode=c-shared -o libsaga_loyalty.so .

# Full rebuild
rebuild: proto build

# Run tests
test: proto
    go test -v ./...

# Clean build artifacts
clean:
    rm -f libsaga_loyalty.so libsaga_loyalty.h
    rm -rf proto/evented/*.go proto/examples/*.go

# Verify the shared library exports
verify: build
    nm -D libsaga_loyalty.so | grep -E "(Handle|Name|Domains|IsSynchronous)"
