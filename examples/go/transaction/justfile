# Transaction Business Logic - Go gRPC Server

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Copy generated proto files
proto:
    mkdir -p proto/angzarr proto/examples
    cp -r "{{TOP}}/generated/go/angzarr/"* proto/angzarr/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/go/examples/"* proto/examples/ 2>/dev/null || true

# Build the binary
build: proto
    go mod tidy
    go build -o transaction .

# Run the server
run: build
    ./transaction

# Run with custom port
run-port port:
    PORT={{port}} ./transaction

# Full rebuild
rebuild: proto build

# Run tests
test: proto
    go test -v ./...

# Clean build artifacts
clean:
    rm -f transaction
    rm -rf proto/angzarr/*.go proto/examples/*.go

# Format code
fmt:
    go fmt ./...

# Lint code
lint:
    golangci-lint run
