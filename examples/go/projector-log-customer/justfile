# Customer Log Projector - Go gRPC Server

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Copy generated proto files
proto:
    mkdir -p proto/evented proto/examples
    cp -r "{{TOP}}/generated/go/evented/"* proto/evented/ 2>/dev/null || true
    cp -r "{{TOP}}/generated/go/examples/"* proto/examples/ 2>/dev/null || true

# Build the binary
build: proto
    go mod tidy
    go build -o projector-log-customer .

# Run the server
run: build
    ./projector-log-customer

# Run with custom port
run-port port:
    PORT={{port}} ./projector-log-customer

# Full rebuild
rebuild: proto build

# Run tests
test: proto
    go test -v ./...

# Clean build artifacts
clean:
    rm -f projector-log-customer
    rm -rf proto/evented/*.go proto/examples/*.go

# Format code
fmt:
    go fmt ./...

# Lint code
lint:
    golangci-lint run
