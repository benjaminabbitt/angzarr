# Go examples - gRPC servers
# Container: podman build -t angzarr-dev-go -f examples/go/Containerfile.dev .
# Run:       podman run --rm -v .:/app:Z -w /app/examples/go angzarr-dev-go just test
#
# Deployment Model:
#   - Backing services (PostgreSQL, RabbitMQ): Deployed via Terraform/Helm from root justfile
#   - Application services: Deployed via Skaffold
#
# For cluster deployment:
#   1. From repo root: `just kind-create-registry` - Create Kind cluster
#   2. From repo root: `just infra-local` - Deploy backing services via Terraform
#   3. Use `skaffold dev` or `skaffold run` in this directory for application services

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# Build dev container
container-build:
    cd {{TOP}} && podman build -t angzarr-dev-go -f examples/go/Containerfile.dev .

# Run tests in container
container-test:
    cd {{TOP}} && podman run --rm -v .:/app:Z -w /app/examples/go angzarr-dev-go just test

# Build all Go examples
build: build-customer build-transaction build-saga-loyalty build-projector-log-customer build-projector-log-transaction

# Build customer bounded context
build-customer:
    cd customer && just build

# Build transaction bounded context
build-transaction:
    cd transaction && just build

# Build saga-loyalty
build-saga-loyalty:
    cd saga-loyalty && just build

# Build projector-log-customer
build-projector-log-customer:
    cd projector-log-customer && just build

# Build projector-log-transaction
build-projector-log-transaction:
    cd projector-log-transaction && just build

# Test all Go examples
test: test-customer test-transaction test-saga-loyalty test-projector-log-customer test-projector-log-transaction

# Test customer
test-customer: build-customer
    cd customer && just test

# Test transaction
test-transaction: build-transaction
    cd transaction && just test

# Test saga-loyalty
test-saga-loyalty: build-saga-loyalty
    cd saga-loyalty && just test || true

# Test projector-log-customer
test-projector-log-customer: build-projector-log-customer
    cd projector-log-customer && just test || true

# Test projector-log-transaction
test-projector-log-transaction: build-projector-log-transaction
    cd projector-log-transaction && just test || true

# Clean all Go examples
clean:
    cd common && just clean || true
    cd customer && just clean || true
    cd transaction && just clean || true
    cd saga-loyalty && just clean || true
    cd projector-log-customer && just clean || true
    cd projector-log-transaction && just clean || true

# Format all Go code
fmt:
    cd customer && go fmt ./...
    cd transaction && go fmt ./...
    cd saga-loyalty && go fmt ./...
    cd projector-log-customer && go fmt ./...
    cd projector-log-transaction && go fmt ./...

# Lint all Go code
lint:
    cd customer && go vet ./...
    cd transaction && go vet ./...
    cd saga-loyalty && go vet ./...
    cd projector-log-customer && go vet ./...
    cd projector-log-transaction && go vet ./...

# Lint Helm chart
helm-lint:
    helm lint helm

