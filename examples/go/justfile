# Go poker examples

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Default: list available commands
default:
    @just --list

# Build all Go aggregates
build:
    cd "{{TOP}}/examples/go" && go build ./...

# Run poker in standalone mode
run: build
    mkdir -p "{{TOP}}/examples/go/data"
    cd "{{TOP}}" && cargo run \
        --bin angzarr-standalone \
        --features standalone,sqlite \
        -- --config examples/go/standalone.yaml

# Run tests
test:
    cd "{{TOP}}/examples/go" && go test -v ./...

# Format code
fmt:
    cd "{{TOP}}/examples/go" && gofmt -w .

# Lint code
lint:
    cd "{{TOP}}/examples/go" && golangci-lint run

# Clean build artifacts
clean:
    rm -rf "{{TOP}}/examples/go/data"
