# Go examples for angzarr e-commerce domain
# Services: customer, product, inventory, order, cart, fulfillment
# Sagas: saga-loyalty-earn, saga-fulfillment, saga-cancellation
#
# Deployment modes (use from examples/ directory):
#   just examples standalone run go     # Run in standalone mode
#   just examples standalone test go    # Test in standalone mode
#   just examples k8s deploy go       # Deploy to K8s
#   just examples k8s test go         # Test in K8s

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`

# Show available commands
default:
    @just --list

# ============================================================================
# Build / Test / Clean
# ============================================================================

# Build all Go services
build:
    for dir in customer product inventory order cart fulfillment saga-fulfillment saga-cancellation saga-loyalty-earn; do \
        echo "Building $dir..."; \
        (cd "{{TOP}}/examples/go/$dir" && go build .) || true; \
    done

# Run unit tests for all services
test:
    for dir in customer product inventory order cart fulfillment; do \
        echo "Testing $dir..."; \
        (cd "{{TOP}}/examples/go/$dir" && go test -v ./...) || true; \
    done

# Clean all Go examples
clean:
    for dir in customer product inventory order cart fulfillment saga-fulfillment saga-cancellation saga-loyalty-earn; do \
        (cd "{{TOP}}/examples/go/$dir" && go clean) 2>/dev/null || true; \
    done

# Format all Go code
fmt:
    for dir in customer product inventory order cart fulfillment saga-fulfillment saga-cancellation saga-loyalty-earn; do \
        (cd "{{TOP}}/examples/go/$dir" && go fmt ./...) 2>/dev/null || true; \
    done

# Lint all Go code
lint:
    for dir in customer product inventory order cart fulfillment saga-fulfillment saga-cancellation saga-loyalty-earn; do \
        (cd "{{TOP}}/examples/go/$dir" && go vet ./...) 2>/dev/null || true; \
    done

# Lint Helm chart
helm-lint:
    helm lint helm

# ============================================================================
# Container Images
# ============================================================================

# Build all Go service images (no cache) and push to local registry
images-push:
    #!/usr/bin/env bash
    set -e
    REGISTRY="localhost:5001"
    TARGETS=(
        "order:aggregate-order-go"
        "inventory:aggregate-inventory-go"
        "fulfillment:aggregate-fulfillment-go"
        "saga-fulfillment:saga-order-fulfillment-go"
        "saga-inventory-reservation:saga-order-inventory-go"
        "sag-fulfillment-inventory:saga-fulfillment-inventory-go"
        "projector-inventory:projector-inventory-go"
        "process-manager-fulfillment:process-manager-fulfillment-go"
    )
    cd {{TOP}}
    for entry in "${TARGETS[@]}"; do
        target="${entry%%:*}"
        image="${entry##*:}"
        echo "=== Building $image ==="
        podman build --no-cache -f examples/go/Containerfile --target "$target" -t "$REGISTRY/$image:latest" .
        echo "=== Pushing $image ==="
        podman push "$REGISTRY/$image:latest" --tls-verify=false
    done
    echo "All Go images built and pushed."

# Build dev container
container-build:
    cd {{TOP}} && podman build -t angzarr-dev-go -f examples/go/Containerfile.dev .

# Run tests in container
container-test:
    cd {{TOP}} && podman run --rm -v .:/app:Z -w /app/examples/go angzarr-dev-go just test

# ============================================================================
# Standalone Mode Shortcuts (delegates to examples/standalone.justfile)
# ============================================================================

# Run in standalone mode
standalone:
    cd {{TOP}}/examples && just standalone run go

# Run acceptance tests in standalone mode
acceptance-standalone:
    cd {{TOP}}/examples && just standalone test go

# ============================================================================
# K8s Mode Shortcuts (delegates to examples/k8s.justfile)
# ============================================================================

# Deploy to K8s
deploy:
    cd {{TOP}}/examples && just k8s deploy go

# Run acceptance tests in K8s
acceptance-k8s:
    cd {{TOP}}/examples && just k8s test go

# Start dev mode with file watching
dev:
    cd {{TOP}}/examples && just k8s dev go

# Delete K8s deployment
delete:
    cd {{TOP}}/examples && just k8s delete go

