# Java client library commands
#
# Container Overlay Pattern:
# --------------------------
# This justfile uses an overlay pattern for container execution:
#
# 1. `justfile` (this file) - runs on the host, delegates to container
# 2. `justfile.container` - mounted over this file inside the container
#
# When running outside a devcontainer:
#   - Uses pre-built angzarr-java image from ghcr.io/angzarr
#   - Podman mounts justfile.container as /workspace/client/java/justfile
#
# When running inside a devcontainer (DEVCONTAINER=true):
#   - Commands execute directly via `just <target>`
#   - No container nesting

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE := "ghcr.io/angzarr-io/angzarr-java:latest"

# Run just target in container (or directly if already in devcontainer)
[private]
_container +ARGS:
    #!/usr/bin/env bash
    if [ "${DEVCONTAINER:-}" = "true" ]; then
        just {{ARGS}}
    else
        podman run --rm --network=host \
            -v "{{TOP}}:/workspace:Z" \
            -v "{{TOP}}/client/java/justfile.container:/workspace/client/java/justfile:ro" \
            -w /workspace/client/java \
            -e DEVCONTAINER=true \
            {{IMAGE}} just {{ARGS}}
    fi

default:
    @just --list

proto:
    just _container proto

build:
    just _container build

test:
    just _container test

coverage:
    just _container coverage

publish:
    just _container publish

# Publish to local Maven repository for testing
publish-local:
    just _container publish-local

clean:
    rm -rf "{{TOP}}/client/java/build" "{{TOP}}/client/java/proto/build" "{{TOP}}/client/java/client/build" "{{TOP}}/client/java/.gradle"
