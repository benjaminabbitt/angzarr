# Go client library commands
#
# Container Overlay Pattern:
# --------------------------
# This justfile uses an overlay pattern for container execution:
#
# 1. `justfile` (this file) - runs on the host, delegates to container
# 2. `justfile.container` - mounted over this file inside the container
#
# When running outside a devcontainer:
#   - Uses pre-built angzarr-go image from ghcr.io/angzarr
#   - Podman mounts justfile.container as /workspace/client/go/justfile
#
# When running inside a devcontainer (DEVCONTAINER=true):
#   - Commands execute directly via `just <target>`
#   - No container nesting

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE := "ghcr.io/angzarr-io/angzarr-go:latest"

# Run just target in container (or directly if already in devcontainer)
[private]
_container +ARGS:
    #!/usr/bin/env bash
    if [ "${DEVCONTAINER:-}" = "true" ]; then
        just {{ARGS}}
    else
        podman run --rm --network=host \
            -v "{{TOP}}:/workspace:Z" \
            -v "{{TOP}}/client/go/justfile.container:/workspace/client/go/justfile:ro" \
            -w /workspace/client/go \
            -e DEVCONTAINER=true \
            {{IMAGE}} just {{ARGS}}
    fi

default:
    @just --list

proto:
    just _container proto

build:
    just _container build

test:
    just _container test

coverage:
    just _container coverage

# Show coverage in browser (host only)
coverage-html:
    just _container coverage
    go tool cover -html="{{TOP}}/client/go/coverage.out"

mutate:
    just _container mutate

mutate-dry:
    just _container mutate-dry

clean:
    rm -f "{{TOP}}/client/go/coverage.out"
