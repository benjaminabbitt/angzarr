# Python client library commands
#
# Container Overlay Pattern:
# --------------------------
# This justfile uses an overlay pattern for container execution:
#
# 1. `justfile` (this file) - runs on the host, delegates to container
# 2. `justfile.container` - mounted over this file inside the container
#
# When running outside a devcontainer:
#   - Uses pre-built angzarr-python image from ghcr.io/angzarr
#   - Podman mounts justfile.container as /workspace/client/python/justfile
#
# When running inside a devcontainer (DEVCONTAINER=true):
#   - Commands execute directly via `just <target>`
#   - No container nesting

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE := "ghcr.io/angzarr/angzarr-python:latest"

# Run just target in container (or directly if already in devcontainer)
[private]
_container +ARGS:
    #!/usr/bin/env bash
    if [ "${DEVCONTAINER:-}" = "true" ]; then
        just {{ARGS}}
    else
        podman run --rm --network=host \
            -v "{{TOP}}:/workspace:Z" \
            -v "{{TOP}}/client/python/justfile.container:/workspace/client/python/justfile:ro" \
            -w /workspace/client/python \
            -e DEVCONTAINER=true \
            {{IMAGE}} just {{ARGS}}
    fi

default:
    @just --list

proto:
    just _container proto

test:
    just _container test

coverage:
    just _container coverage

mutate:
    just _container mutate

mutate-results:
    just _container mutate-results

mutate-html:
    just _container mutate-html

build:
    just _container build

publish-test:
    just _container publish-test

publish:
    just _container publish

clean:
    rm -rf "{{TOP}}/client/python/dist" "{{TOP}}/client/python/build" "{{TOP}}/client/python/*.egg-info" "{{TOP}}/client/python/htmlcov" "{{TOP}}/client/python/.mutmut-cache"
