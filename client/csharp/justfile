# C# client library commands
#
# Container Overlay Pattern:
# --------------------------
# This justfile uses an overlay pattern for container execution:
#
# 1. `justfile` (this file) - runs on the host, delegates to container
# 2. `justfile.container` - mounted over this file inside the container
#
# When running outside a devcontainer:
#   - Builds/uses local devcontainer image with `just` pre-installed
#   - Podman mounts justfile.container as /workspace/client/csharp/justfile
#
# When running inside a devcontainer (DEVCONTAINER=true):
#   - Commands execute directly via `just <target>`
#   - No container nesting

set shell := ["bash", "-c"]

TOP := `git rev-parse --show-toplevel`
IMAGE := "angzarr-client-csharp-dev"

# Build the devcontainer image
[private]
_build-image:
    podman build -t {{IMAGE}} -f "{{TOP}}/client/csharp/.devcontainer/Containerfile" "{{TOP}}/client/csharp/.devcontainer"

# Run just target in container (or directly if already in devcontainer)
[private]
_container +ARGS: _build-image
    #!/usr/bin/env bash
    if [ "${DEVCONTAINER:-}" = "true" ]; then
        just {{ARGS}}
    else
        podman run --rm \
            -v "{{TOP}}:/workspace:Z" \
            -v "{{TOP}}/client/csharp/justfile.container:/workspace/client/csharp/justfile:ro" \
            -w /workspace/client/csharp \
            {{IMAGE}} just {{ARGS}}
    fi

default:
    @just --list

proto:
    just _container proto

build:
    just _container build

test:
    just _container test

coverage:
    just _container coverage

pack:
    just _container pack

publish:
    just _container publish

clean:
    rm -rf "{{TOP}}/client/csharp/Angzarr.Client/bin" "{{TOP}}/client/csharp/Angzarr.Client/obj"
